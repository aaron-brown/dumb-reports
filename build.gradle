/*
 * Copyright 2020 Aaron Brown
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */
plugins {
	id 'idea'
	id 'java'
	id 'java-library'
	id 'groovy'
}

configure(subprojects) {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'groovy'

	sourceCompatibility = 1.8

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
		testImplementation 'org.objenesis:objenesis:1.4'
		testImplementation "cglib:cglib:2.2"
	}

	if (name.startsWith('spring-')) {
		dependencies {
			api platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
			api 'org.springframework.boot:spring-boot-starter'

			annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    		annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

			testImplementation 'org.spockframework:spock-spring:1.3-groovy-2.5'
			testImplementation 'org.springframework.boot:spring-boot-starter-test'
		}
	}
}

configure(subprojects.findAll { !['spring-dumb-reports-example'].contains(it.name) }) {
	task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}
